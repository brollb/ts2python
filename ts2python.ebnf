# interfaces subest of typescript-grammar
# for typescript see: https://www.typescriptlang.org/docs/
# for examples of typescript-interfaces
# see: https://microsoft.github.io/language-server-protocol/specifications/specification-current/

#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = /\s*/           # implicit whitespace, includes linefeeds
@ literalws   = right           # literals have implicit whitespace on the right hand side
@ comment     = /(?:\/\/.*)|(?:\/\*(?:.|\n)*?\*\/)/   # /* ... */ or // to EOL
@ ignorecase  = False           # literals and regular expressions are case-sensitive
@ reduction   = merge_treetops  # anonymous nodes are being reduced where possible
@ disposable  = INT, NEG, FRAC, DOT, EXP, EOF,
                _array_ellipsis, _top_level_assignment, _top_level_literal,
                _quoted_identifier, _root
@ drop        = whitespace, strings, EOF,
                _array_ellipsis, _top_level_assignment, _top_level_literal


#######################################################################
#
#:  Typescript Document
#
#######################################################################

_root       = document EOF
document    = ~ { interface | type_alias | namespace | enum | const
                | declaration ";" | _top_level_assignment
                | _array_ellipsis | _top_level_literal | module }
module      = "declare" "module" _quoted_identifier "{" document "}"


#######################################################################
#
#:  Interfaces
#
#######################################################################

interface   = ["export"] ("interface"|"class") §identifier [type_parameters]
              [extends] declarations_block
extends     = "extends" identifier { "," identifier}
type_alias  = ["export"] "type" §identifier "=" types ";"

declarations_block = "{" [(function|declaration) { [";"] (function|declaration) }
                          [";" map_signature] [";"]] "}"

declaration = { qualifier } identifier [optional] !`(` [":" types]
  qualifier = "readonly" | "static"
  optional  = "?"
  index_signature = "[" identifier (":" | "in" "keyof") type "]"

function    = identifier "(" [arg_list] ")" [":" types] ";"
  arg_list  = argument { "," argument }
  argument  = identifier [optional] [":" types]  # same as declaration ?


#######################################################################
#
#:  Types
#
#######################################################################

types       = type { "|" type }
type        = array_of | basic_type | generic_type | type_name | "(" types ")"
             | mapped_type | declarations_block | type_tuple | literal
             | func_type
generic_type = identifier type_parameters
type_parameters = "<" parameter_types { "," parameter_types } ">"
parameter_types = (generic_type | type_name)  { "|" (generic_type | type_name) }
type_name   = identifier !`<`
array_of    = [ "readonly" ] (basic_type | "(" types ")" | generic_type | type_name) "[]"
type_tuple  = "[" type {"," type} "]"
mapped_type = "{" map_signature [";"] "}"
map_signature = index_signature ":" types
func_type   = "(" [arg_list] ")" "=>" types

#######################################################################
#
#:  Namespaces
#
#######################################################################

namespace   = ["export"] "namespace" §identifier "{"
              { interface | type_alias | enum | const
              | declaration ";" }  "}"

#######################################################################
#
#:  Enums
#
#######################################################################

enum        = ["export"] "enum" identifier §"{" item { "," item } [","] "}"
  item      = _quoted_identifier ["=" literal]

#######################################################################
#
#: Consts
#
#######################################################################

const       = ["export"] "const" §declaration ["=" (literal | identifier)] ";"
_top_level_assignment = assignment
assignment  = variable "=" (literal | variable) ";"  # no expressions, yet

#######################################################################
#
#: literals
#
#######################################################################

_array_ellipsis = literal { "," literal }

_top_level_literal = literal
literal    = integer | number | string | array | object
integer    = INT !/[.Ee]/ ~
number     = INT FRAC EXP ~
string     = /"[^"\n]*"/~ | /'[^'\n]*'/~
array      = "[" [ literal { "," literal } ] "]"
object     = "{" [ association { "," association } ] [","] "}"
  association = name ":" literal
  name        = identifier | '"' identifier '"'

#######################################################################
#
#: Keywords
#
#######################################################################

basic_type   = (`object` | `array` | `string` | `number` | `boolean` | `null`
               | `integer` | `uinteger` | `decimal` | `unknown` | `any` ) ~

#######################################################################
#
#: Entities
#
#######################################################################

variable   = identifier { `.` identifier }
_quoted_identifier = identifier | '"' identifier §'"' | "'" identifier §"'"
identifier = /(?!\d)\w+/~

INT         = [NEG] ( /[1-9][0-9]+/ | /[0-9]/ )
NEG         = `-`
FRAC        = [ DOT /[0-9]+/ ]
DOT         = `.`
EXP         = [ (`E`|`e`) [`+`|`-`] /[0-9]+/ ]

EOF        =  !/./        # no more characters ahead, end of file reached
